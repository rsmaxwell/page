
ext {
    TIMESTAMP = new Date().format('yyyy-MM-dd HH:mm:ss.SSS')

    new ByteArrayOutputStream().withStream { os ->
        def result = exec {
            commandLine = [ 'go', 'env', 'GOEXE' ]
            standardOutput = os
        }
        GOEXE = os.toString().trim()
    }

    new ByteArrayOutputStream().withStream { os ->
        def result = exec {
            commandLine = [ 'go', 'env', 'GOOS' ]
            standardOutput = os
        }
        GOOS = os.toString().trim()
    }

    new ByteArrayOutputStream().withStream { os ->
        def result = exec {
            commandLine = [ 'go', 'env', 'GOARCH' ]
            standardOutput = os
        }
        GOARCH = os.toString().trim()
    }
            
    mainName = "page"

    groupID = 'com.rsmaxwell.diary'
    artifactID = "${mainName}-${GOARCH}-${GOOS}"
    packaging = "zip"
    internalTestClassifier = "internalTest"
    stagingTestClassifier = "stagingTest"
    releasesTestClassifier = "releasesTest"

    buildID = System.getenv("BUILD_ID") ?: "SNAPSHOT"
    myVersion = "0.0.${buildID}"
}

task generate {
    dependsOn 'updateVersionGO'
    dependsOn 'writeVersionJSON'
}

task updateVersionGO {
    onlyIf { System.env.BUILD_ID != null }
    doLast {
        ant.replace(dir: 'internal', includes: '**/version.go', token: '<VERSION>', value: myVersion)
        ant.replace(dir: 'internal', includes: '**/version.go', token: '<BUILD_ID>', value: System.env.BUILD_ID)
        ant.replace(dir: 'internal', includes: '**/version.go', token: '<TIMESTAMP>', value: TIMESTAMP)
        ant.replace(dir: 'internal', includes: '**/version.go', token: '<GIT_COMMIT>', value: System.env.GIT_COMMIT)
        ant.replace(dir: 'internal', includes: '**/version.go', token: '<GIT_BRANCH>', value: System.env.GIT_BRANCH)
        ant.replace(dir: 'internal', includes: '**/version.go', token: '<GIT_URL>', value: System.env.GIT_URL)
    }
}

task writeVersionJSON {
    onlyIf { System.env.BUILD_ID != null }
    doLast {
        def data = [
            version:      myVersion,
            build_id:     System.env.BUILD_ID,
            buildDate:    TIMESTAMP,
            gitCommit:    System.env.GIT_COMMIT,
            gitBranch:    System.env.GIT_BRANCH,
            gitURL:       System.env.GIT_URL
        ]

        def json_str = groovy.json.JsonOutput.toJson(data)
        def json_beauty = groovy.json.JsonOutput.prettyPrint(json_str)
        new File("build").mkdir()
        File file = new File( 'build/version.json' )
        file.write(json_beauty)
    }
}

task build()  {
    doLast {
        exec {
            commandLine 'go', 'build', '-o', "./build/${mainName}-${GOARCH}-${GOOS}-${myVersion}${GOEXE}", "github.com/rsmaxwell/page/cmd/${mainName}"
        }
        exec {
            commandLine 'go', 'test', './...'
        }
    }
}

task clean() {
    doLast {
        delete fileTree('./build') {
            include '**/*'
        }
    }
}

task zip {
    dependsOn('zipMain')
}

task zipMain(type: Zip) {
    from('build') {
        include "${mainName}-${GOARCH}-${GOOS}-${myVersion}${GOEXE}"
        include 'version.json'
    }
    archiveName "${mainName}-${GOARCH}-${GOOS}-${myVersion}.${packaging}"
    destinationDir(file('./build'))
}







apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            groupId groupID
            artifactId artifactID
            version myVersion

            artifact("./build/${mainName}-${GOARCH}-${GOOS}-${myVersion}.${packaging}") 
        }
    }
    repositories {
        maven {
            name 'releases'
            url "${repoUrl}/releases"
            credentials {
                username = repoUsername
                password = repoPassword
            }
        }
    }
}
